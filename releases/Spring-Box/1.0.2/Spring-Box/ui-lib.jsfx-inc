@init
uix_debug_layout = 0;
uix_round = 1;

//uix_hassetup = 0;
//uix_screenstack = 0;
uix_screenstack_step = 10 + 1/*UI_SCREEN: ID*/; // header + 10 vars
uix_screenstack_layercount = 10;
//uix_screenstack_currentlayer = 0;

//uix_drawconfig = 0;
uix_drawconfig_step = 20/*uix_DRAWCONFIG: RED, GREEN, BLUE, ALPHA, FONTBOLD, FONTITALIC, FONTINDEX, FONTFACE, FONTSIZE, LEFT, TOP, WIDTH, HEIGHT, HALIGN, VALIGN, HPADDING, VPADDING, SPLIT_DIRECTION, SPLIT_HEIGHT, SPLIT_WIDTH*/;
uix_drawconfig_count = 100;
//uix_drawconfig_current = 0;
//uix_drawconfig_currentindex = -1;

//uix_reserved_end = 0;

//uix_mousecap_prev = 0;
uix_mouseblocked = 0;
uix_doubleclicktime = 0.2;
uix_draglimit = 10;
uix_click1 = uix_click2 = uix_click64 = 0;
uix_unclick1 = uix_unclick2 = uix_unclick64 = 0;
uix_click1_x = uix_click1_y = uix_click1_t = 0; // Left
uix_click2_x = uix_click2_y = uix_click2_t = 0; // Right
uix_click64_x = uix_click64_y = uix_click3_t = 0; // Middle
uix_mouse_x = uix_prev_mouse_x = 0;
uix_mouse_y = uix_prev_mouse_y = 0;

uix_default_fontsize = 16;
uix_default_fontface = "Arial";
uix_font_dirty = 0;
uix_latestchar = 0;
uix_char_consumed = 0;

uix_error = 0;
function uix_clamp1(v) (
	min(1, max(0, v));
);
function uix_clamp255(v) (
	min(255, max(0, floor(v + 0.5)));
);
function uix_setgfxcolorrgba(r, g, b, a) (
	gfx_r = uix_clamp1(r/255);
	gfx_g = uix_clamp1(g/255);
	gfx_b = uix_clamp1(b/255);
	gfx_a = uix_clamp1(a);
);
function uix_setgfxcolor() (
	uix_setgfxcolorrgba(
		uix_drawconfig_current[0/*uix_DRAWCONFIG:RED*/],
		uix_drawconfig_current[1/*uix_DRAWCONFIG:GREEN*/],
		uix_drawconfig_current[2/*uix_DRAWCONFIG:BLUE*/],
		uix_drawconfig_current[3/*uix_DRAWCONFIG:ALPHA*/]
	);
);
function uix_clearcolor(r, g, b) (
	uix_clamp255(r) + 256*uix_clamp255(g) + 65536*uix_clamp255(b);
);
function uix_fontflags() local(bold, italic) (
	bold = uix_drawconfig_current[4/*uix_DRAWCONFIG:FONTBOLD*/];
	italic = uix_drawconfig_current[5/*uix_DRAWCONFIG:FONTITALIC*/];
	bold ? (
		italic ? 'bi' : 'b';
	) : (
		italic ? 'i' : '';
	);
);
function uix_setgfxfont() (
	uix_font_dirty || gfx_getfont() != uix_drawconfig_current[6/*uix_DRAWCONFIG:FONTINDEX*/] ? (
		uix_font_dirty = 0;
		gfx_setfont(
			uix_drawconfig_current[6/*uix_DRAWCONFIG:FONTINDEX*/],
			uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/],
			uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/],
			uix_fontflags()
		);
	);
);
function uix_click_block() (
	uix_mouseblocked = 1;
	uix_click1count = 0;
	uix_click1 = uix_click2 = uix_click64 = 0;
	uix_unclick1 = uix_unclick2 = uix_unclick64 = 0;
	uix_click1_x = uix_click1_y = uix_click1_t = 0; // Left
	uix_click2_x = uix_click2_y = uix_click2_t = 0; // Right
	uix_click64_x = uix_click64_y = uix_click3_t = 0; // Middle
);
function uix_key_clear() (
	while (
		gfx_getchar() > 0;
	);
);

function ui_error(string) (
	uix_click_block();
	uix_key_clear();
	uix_screenstack_currentlayer = 0;
	uix_screenstack[0/*UI_SCREEN:ID*/] = "error";
	uix_error = uix_screenstack[1/*UI_SCREEN: ID*/] = string; // First argument
	0;
);

function ui_screen() (
	uix_hassetup
		? (uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer)[0]
		: "error";
);

function ui_screen_level() (
	uix_screenstack_currentlayer;
);

function ui_screen_open(id) local(i, screen) (
	uix_click_block();
	uix_key_clear();
	uix_screenstack_currentlayer += 1;
	uix_screenstack_currentlayer < uix_screenstack_layercount ? (
		screen = (uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer);
		screen[0] = id;
		i = 1;
		while (
			screen[i] = 0;
			i += 1;
			i < uix_screenstack_step;
		);
	) : ui_error("Too many screens - only 10 dialogs deep")
);

function ui_screen_close() (
	uix_click_block();
	uix_key_clear();
	uix_screenstack_currentlayer > 0 ? (
		uix_screenstack_currentlayer -= 1;
	) : ui_error("Cannot close top-level screen");
);

function ui_screen_close_id(id) (
	while (uix_screenstack_currentlayer > 0 && ui_screen() == id) (
		ui_screen_close();
	);
);

function ui_screen_close_to(id) (
	while (uix_screenstack_currentlayer > 0 && ui_screen() != id) (
		ui_screen_close();
	);
);

function ui_screen_get(index) local(screen) (
	screen = (uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer);
	offset = index + 1/*UI_SCREEN: ID*/;
	offset < uix_screenstack_step ? (
		screen[offset];
	) : (
		ui_error("Screens only have 10 arguments");
	);
);

function ui_screen_set(index, value) local(screen, offset) (
	screen = (uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer);
	offset = index + 1;
	offset < uix_screenstack_step ? (
		screen[offset] = value;
	) : (
		ui_error("Screens only have 10 arguments");
	);
);

function ui_setup(stackstart) local(uix_screenstack_end, uix_drawconfig_end) (
	!uix_hassetup ? (
		uix_hassetup = 1;
		
		uix_screenstack = stackstart;
		uix_screenstack_currentlayer = 0;
		uix_screenstack_end = stackstart + uix_screenstack_step*uix_screenstack_layercount;
		
		uix_drawconfig = uix_screenstack_end;
		uix_drawconfig_end = uix_drawconfig + uix_drawconfig_step*uix_drawconfig_count;
		uix_reserved_end = uix_drawconfig_end;
		
		// Stop the pre-processor complaining
		4/*UI_SPLIT_DIRECTION: TOP, BOTTOM, LEFT, RIGHT*/;
	);
	uix_reserved_end;
);

function ui_start(defaultScreen) local(i, click, clickdelta) (
	uix_drawconfig_currentindex = 0;
	uix_drawconfig_current = uix_drawconfig;

	uix_hassetup ? (
		!ui_screen() ? (
			uix_screenstack_currentlayer = 0;
			i = 0;
			while (
				uix_screenstack[i] = 0;
				i += 1;
				i < uix_screenstack_step;
			);
			uix_screenstack[0] = defaultScreen;
		);
		
		// Default colours
		gfx_clear = uix_clearcolor(242, 242, 242);
		
		uix_drawconfig_current[0/*uix_DRAWCONFIG:RED*/] = 0;
		uix_drawconfig_current[1/*uix_DRAWCONFIG:GREEN*/] = 0;
		uix_drawconfig_current[2/*uix_DRAWCONFIG:BLUE*/] = 0;
		uix_drawconfig_current[3/*uix_DRAWCONFIG:ALPHA*/] = 1;

		uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] = 0;
		uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] = 0;
		uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = gfx_w;
		uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = gfx_h;
		uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/] = 0.5;
		uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/] = 0.5;
		uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/] = 10;
		uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/] = 8;
		
		uix_drawconfig_current[6/*uix_DRAWCONFIG:FONTINDEX*/] = 16;
		uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/] = uix_default_fontface;
		uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/] = uix_default_fontsize;
		uix_drawconfig_current[4/*uix_DRAWCONFIG:FONTBOLD*/] = 0;
		uix_drawconfig_current[5/*uix_DRAWCONFIG:FONTITALIC*/] = 0;
		
		uix_font_dirty = 1;
		uix_setgfxfont();
		
		uix_prev_mouse_x = uix_mouse_x;
		uix_prev_mouse_y = uix_mouse_y;
		uix_mouse_x = mouse_x;
		uix_mouse_y = mouse_y;
		
		uix_unclick1 = (!(mouse_cap&1) && uix_mousecap_prev&1);
		uix_click1 = (mouse_cap&1 && !(uix_mousecap_prev&1)) ? (
			clickdelta = time_precise() - uix_click1_t;
			uix_click1_x = mouse_x;
			uix_click1_y = mouse_y;
			uix_click1 = 1;
			uix_click1_t = time_precise() - 0.001;

			uix_click1 = 1;
			clickdelta < uix_doubleclicktime ? (
				uix_click1count += 1;
			) : (
				uix_click1count = 1;
			);
			
		);
		uix_unclick2 = (!(mouse_cap&2) && uix_mousecap_prev&2);
		uix_click2 = (mouse_cap&2 && !(uix_mousecap_prev&2)) ? (
			uix_click2 = 1;
			uix_click2_x = mouse_x;
			uix_click2_y = mouse_y;
			uix_click2_t = time_precise() - 0.001;
		);
		uix_unclick64 = (!(mouse_cap&64) && uix_mousecap_prev&64);
		uix_click64 = (mouse_cap&64 && !(uix_mousecap_prev&64)) ? (
			uix_click64 = 1;
			uix_click64_x = mouse_x;
			uix_click64_y = mouse_y;
			uix_click64_t = time_precise() - 0.001;
		);
		uix_mousecap_prev = mouse_cap;
		
		uix_mouseblocked ? uix_click_block(); // Must be first, so unclick events get removed too
		!(mouse_cap&1) && !(mouse_cap&2) && !(mouse_cap&64) ? uix_mouseblocked = 0;
		
		// Keyboard
		!uix_latestchar || !uix_char_consumed ? (
			uix_latestchar = gfx_getchar();
			uix_latestchar == -1 ? uix_latestchar = 0;
		);
		uix_char_consumed = 0;
	) : (
		uix_error = "ui_setup() has not been called";
	);
);

function ui_system() local(text, error, screen) (
	ui_screen() == "error" ? (
		gfx_clear = 255 + 255*256 + 255*65536;
		gfx_r = 64;
		gfx_g = gfx_b = 0;
		gfx_x = gfx_y = gfx_texth;
		screen = uix_screenstack + uix_screenstack_step*uix_screenstack_currentlayer;
		error = screen[1/*UI_SCREEN: ID*/]; // First argument
		strlen(error) ? (
			gfx_printf("Error: %s\n(%f)", error, error);
		) : (
			gfx_printf("Error: #%s", error);
		);
		uix_click1 ? (
			uix_screenstack_currentlayer = -1;
			ui_screen_open(0);
		);
	) : (
		gfx_clear = 255 + 255*256 + 255*65536;
		gfx_r = gfx_g = gfx_b = 0;
		gfx_x = gfx_y = gfx_texth;
		ui_screen() ? (
			gfx_printf("Unknown screen: %s (%f)", ui_screen(), ui_screen());
			uix_click1 ? (
				uix_screenstack_currentlayer = -1;
				ui_screen_open(0);
			);
		) : (
			gfx_drawstr("Current screen is 0 - are you calling ui_start() first?");
		);
	);
);

function ui_left() (
	uix_round ? (
		floor(uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] + 0.5);
	) : (
		uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/];
	);
);
function ui_top() (
	uix_round ? (
		floor(uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] + 0.5);
	) : (
		uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/];
	);
);
function ui_bottom() (
	uix_round ? (
		floor(uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] + uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] + 0.5);
	) : (
		uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] + uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]
	);
);
function ui_right() (
	uix_round ? (
		floor(uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] + uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] + 0.5);
	) : (
		uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] + uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/];
	);
);
function ui_width() (
	uix_round ? (
		ui_right() - ui_left();
	) : (
		uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/];
	);
);
function ui_height() (
	uix_round ? (
		ui_bottom() - ui_top();
	) : (
		uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/];
	);
);

/////// Drawing operations ///////

function ui_color_refresh() (
	uix_setgfxcolor();
);

function ui_color(r, g, b) (
	uix_drawconfig_current[0/*uix_DRAWCONFIG:RED*/] = r;
	uix_drawconfig_current[1/*uix_DRAWCONFIG:GREEN*/] = g;
	uix_drawconfig_current[2/*uix_DRAWCONFIG:BLUE*/] = b;
	uix_drawconfig_current[3/*uix_DRAWCONFIG:ALPHA*/] = 1;
	uix_setgfxcolor();
);

function ui_colora(r, g, b, a) (
	uix_drawconfig_current[0/*uix_DRAWCONFIG:RED*/] = r;
	uix_drawconfig_current[1/*uix_DRAWCONFIG:GREEN*/] = g;
	uix_drawconfig_current[2/*uix_DRAWCONFIG:BLUE*/] = b;
	uix_drawconfig_current[3/*uix_DRAWCONFIG:ALPHA*/] = a;
	uix_setgfxcolor();
);

function ui_align(halign, valign) (
	halign >= 0 && halign <= 1 ? uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/] = halign;
	valign >= 0 && valign <= 1 ? uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/] = valign;
);

function ui_halign() (
	uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
);

function ui_valign() (
	uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
);

function ui_fontsize(size) (
	uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/] = size ? size : uix_default_fontsize;
	uix_font_dirty = 1;
	uix_setgfxfont();
);

function ui_fontface(face) (
	uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/] = face;
	uix_font_dirty = 1;
	uix_setgfxfont();
);

function ui_fontbold(isBold) (
	uix_drawconfig_current[4/*uix_DRAWCONFIG:FONTBOLD*/] = isBold;
	uix_font_dirty = 1;
	uix_setgfxfont();
);

function ui_fontitalic(isItalic) (
	uix_drawconfig_current[5/*uix_DRAWCONFIG:FONTITALIC*/] = isItalic;
	uix_font_dirty = 1;
	uix_setgfxfont();
);

function ui_font(face, size, isBold, isItalic) (
	face = face >= 0 ? face : uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/];
	size = size >= 0 ? size : uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/];

	uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/] = face;
	uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/] = size;
	uix_drawconfig_current[4/*uix_DRAWCONFIG:FONTBOLD*/] = isBold;
	uix_drawconfig_current[5/*uix_DRAWCONFIG:FONTITALIC*/] = isItalic;
	uix_font_dirty = 1;
	uix_setgfxfont();
);

function ui_font_refresh() (
	uix_setgfxcolor();
	uix_setgfxfont();
);

function ui_fill() (
	uix_setgfxcolor();
	gfx_rect(ui_left(), ui_top(), ui_width(), ui_height());
);

function ui_texth() (
	uix_setgfxfont();
	gfx_texth;
);

function ui_text_width(text) local(text_h, text_w) (
	text >= 0 ? (
		text_w = text_h = 0;
		uix_setgfxfont();
		gfx_measurestr(text, text_w, text_h);
		text_w;
	) : 0;
);

function ui_text_height(text) local(text_h, text_w) (
	text >= 0 ? (
		uix_setgfxfont();
		gfx_measurestr(text, text_w, text_h);
		text_h;
	) : ui_texth();
);

function ui_text(text) local (text_h, text_w, boxwidth, boxheight, valign, halign, fontscale, fontsize) (
	uix_setgfxcolor();
	uix_setgfxfont();
	valign = uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
	halign = uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
	
	gfx_measurestr(text, text_w, text_h);
	
	boxwidth = ui_width();
	boxheight = ui_height();
	
	fontscale = 1;
	text_w > boxwidth ? (
		fontscale = boxwidth/text_w;
	);
	text_h > boxheight ? (
		fontscale = min(boxheight/text_h, fontscale);
	);
	fontscale != 1 ? (
		fontsize = max(8, floor(fontscale*uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/]));
		fontscale = fontsize/uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/];
		gfx_setfont(
			16,
			uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/],
			fontsize,
			uix_fontflags()
		);
	);
	
	valign = uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
	halign = uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
	gfx_x = ui_left() + (boxwidth - text_w*fontscale)*halign;
	gfx_y = ui_top() + (boxheight - text_h*fontscale)*valign;
	
	gfx_drawstr(text);
	text_w;
);

function ui_textnumber(number, format) local(text) (
	sprintf(#uix_textnumber, format ? format : "%f", number);
	ui_text(#uix_textnumber);
);

function uix_fitline(text, start, text_end, width) local(i, result, line, line_end, char, hadspace, overflowed, text_w, text_h) (
	line = #;
	strcpy(line, "");
	line_end = 0;
	hadspace = 0;
	overflowed = 0;
	i = result = start;
	i < text_end ? while (
		char = str_getchar(text, i);
		char == 10 ? (
			result = i;
			overflowed = 1;
		) : (
			!hadspace ? result = i;
			char == 32 || char == 9 ? (
				hadspace = 1;
				gfx_measurestr(line, text_w, text_h);
				overflowed = text_w > width;
				!overflowed ? result = i;
			);
			str_setchar(line, line_end, char);
			line_end += 1;
		);
		i += 1;
		!overflowed && i < text_end;
	);
	overflowed ? result : text_end;
);

function ui_wraptext_height(text) local(text_length, line_start, line_end, width, height) (
	uix_setgfxfont();
	width = ui_width();
	height = 0;
	
	text_length = strlen(text);
	line_start = 0;
	line_start < text_length ? while (
		line_end = uix_fitline(text, line_start, text_length, width);
		height += gfx_texth;
		line_start = line_end + 1;
		line_start < text_length;
	);
	height;
);

function ui_wraptext(text) local(text_length, line_start, line_end, width, height, line, halign, valign, text_h, text_w) (
	uix_setgfxcolor();
	uix_setgfxfont();
	width = ui_width();
	height = 0;

	halign = uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
	valign = uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
	
	gfx_y = valign ? (
		ui_top() + (ui_height() - ui_wraptext_height(text))*valign;
	) : ui_top();
	
	text_length = strlen(text);
	line_start = 0;
	line_start < text_length ? while (
		line_end = uix_fitline(text, line_start, text_length, width);
		line = #;
		strcpy_substr(line, text, line_start, line_end - line_start);
		gfx_measurestr(line, text_w, text_h);
		gfx_x = ui_left() + (ui_width() - text_w)*halign;
		gfx_drawstr(line);
		gfx_y += gfx_texth;
		height += gfx_texth;
		line_start = line_end + 1;
		line_start < text_length;
	);
	height;
);

function ui_border_top() (
	uix_setgfxcolor();
	gfx_line(ui_left(), ui_top(), ui_right() - 1, ui_top());
);

function ui_border_bottom() (
	uix_setgfxcolor();
	gfx_line(ui_left(), ui_bottom() - 1, ui_right() - 1, ui_bottom() - 1);
);

function ui_border_left() (
	uix_setgfxcolor();
	gfx_line(ui_left(), ui_top(), ui_left(), ui_bottom() - 1);
);

function ui_border_right() (
	uix_setgfxcolor();
	gfx_line(ui_right() - 1, ui_top(), ui_right() - 1, ui_bottom() - 1);
);

function ui_border() (
	uix_setgfxcolor();
	gfx_x = ui_left();
	gfx_y = ui_top();
	gfx_lineto(gfx_x + ui_width() - 1, gfx_y, 0);
	gfx_lineto(gfx_x, gfx_y + ui_height() - 1, 0);
	gfx_lineto(ui_left(), gfx_y, 0);
	gfx_lineto(gfx_x, ui_top(), 0);
);

///// Viewport stack operations /////

function ui_push() local(i, oldlayer) (
	uix_drawconfig_currentindex += 1;
	uix_drawconfig_currentindex < uix_drawconfig_count ? (
		i = 0;
		oldlayer = uix_drawconfig_current;
		uix_drawconfig_current = uix_drawconfig + uix_drawconfig_currentindex*uix_drawconfig_step;
		while (
			uix_drawconfig_current[i] = oldlayer[i];
			i += 1;
			i < uix_drawconfig_step;
		);
		// Some things shouldn't just be copied
		uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/] = -1;
		1;
	);
	0;
);
function ui_pop() local(prev) (
	uix_debug_layout ? (
		ui_color(
			128 + 255*cos((uix_drawconfig_currentindex + 0 + time())*$pi/3),
			128 + 255*cos((uix_drawconfig_currentindex + 2 + time())*$pi/3),
			128 + 255*cos((uix_drawconfig_currentindex + 4 + time())*$pi/3)
		);
		ui_border();
	);

	prev = uix_drawconfig_current;
	uix_drawconfig_currentindex > 0 ? (
		uix_drawconfig_currentindex -= 1;
		uix_drawconfig_current = uix_drawconfig + uix_drawconfig_currentindex*uix_drawconfig_step;
		
		prev[6/*uix_DRAWCONFIG:FONTINDEX*/] != uix_drawconfig_current[6/*uix_DRAWCONFIG:FONTINDEX*/] ? uix_font_dirty = 1;
		prev[7/*uix_DRAWCONFIG:FONTFACE*/] != uix_drawconfig_current[7/*uix_DRAWCONFIG:FONTFACE*/] ? uix_font_dirty = 1;
		prev[8/*uix_DRAWCONFIG:FONTSIZE*/] != uix_drawconfig_current[8/*uix_DRAWCONFIG:FONTSIZE*/] ? uix_font_dirty = 1;
		prev[4/*uix_DRAWCONFIG:FONTBOLD*/] != uix_drawconfig_current[4/*uix_DRAWCONFIG:FONTBOLD*/] ? uix_font_dirty = 1;
		prev[5/*uix_DRAWCONFIG:FONTITALIC*/] != uix_drawconfig_current[5/*uix_DRAWCONFIG:FONTITALIC*/] ? uix_font_dirty = 1;
		uix_font_dirty ? uix_setgfxfont();
		1;
	);
	0;
);
function ui_split_top(height) local(oldTop) (
	height == -1 ? height = ui_text() + uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]*2;

	height = min(height, uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
	oldTop = uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/];
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] += height;
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] -= height;
	ui_push();
	uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/] = 0/*UI_SPLIT_DIRECTION:TOP*/;
	uix_drawconfig_current[18/*uix_DRAWCONFIG:SPLIT_HEIGHT*/] = height;
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] = oldTop;
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = height;
	height;
);
function ui_split_topratio(ratio) (
	ui_split_top(ratio*uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
);
function ui_split_toptext(text) local(text_height) (
	text_height = max(ui_texth(), ui_text_height(text));
	ui_split_top(text_height + 2*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]);
);
function ui_split_bottom(height) local(oldBottom) (
	height == -1 ? height = ui_text() + uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]*2;
	height = min(height, uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
	oldBottom = uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] + uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/];
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] -= height;
	ui_push();
	uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/] = 1/*UI_SPLIT_DIRECTION:BOTTOM*/;
	uix_drawconfig_current[18/*uix_DRAWCONFIG:SPLIT_HEIGHT*/] = height;
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] = oldBottom - height;
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = height;
	height;
);
function ui_split_bottomratio(ratio) (
	ui_split_bottom(ratio*uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
);
function ui_split_bottomtext(text) (
	text_height = max(ui_texth(), ui_text_height(text));
	ui_split_bottom(text_height + 2*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]);
);
function ui_split_left(width) local(oldLeft) (
	width == -1 ? width = ui_text()*4 + uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]*2;
	width = min(width, uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
	oldLeft = uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/];
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] += width;
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] -= width;
	ui_push();
	uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/] = 2/*UI_SPLIT_DIRECTION:LEFT*/;
	uix_drawconfig_current[19/*uix_DRAWCONFIG:SPLIT_WIDTH*/] = width;
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] = oldLeft;
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = width;
	width;
);
function ui_split_leftratio(ratio) (
	ui_split_left(ratio*uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
);
function ui_split_lefttext(text) (
	ui_split_left(ui_text_width(text) + 2*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]);
);
function ui_split_right(width) local(oldRight) (
	width == -1 ? width = ui_text()*4 + uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]*2;
	width = min(width, uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
	oldRight = uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] + uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/];
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] -= width;
	ui_push();
	uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/] = 3/*UI_SPLIT_DIRECTION:RIGHT*/;
	uix_drawconfig_current[19/*uix_DRAWCONFIG:SPLIT_WIDTH*/] = width;
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] = oldRight - width;
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = width;
	width;
);
function ui_split_rightratio(ratio) (
	ui_split_right(ratio*uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
);
function ui_split_righttext(text) (
	ui_split_right(ui_text_width(text) + 2*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]);
);

function ui_split_next() local(direction, width, height) (
	direction = uix_drawconfig_current[17/*uix_DRAWCONFIG:SPLIT_DIRECTION*/];
	width = uix_drawconfig_current[19/*uix_DRAWCONFIG:SPLIT_WIDTH*/];
	height = uix_drawconfig_current[18/*uix_DRAWCONFIG:SPLIT_HEIGHT*/];
	ui_pop();
	direction == 2/*UI_SPLIT_DIRECTION:LEFT*/ ? (
		ui_split_left(width);
	) : direction == 0/*UI_SPLIT_DIRECTION:TOP*/ ? (
		ui_split_top(height);
	) : direction == 3/*UI_SPLIT_DIRECTION:RIGHT*/ ? (
		ui_split_right(width);
	) : direction == 1/*UI_SPLIT_DIRECTION:BOTTOM*/ ? (
		ui_split_bottom(height);
	) : (
		ui_error("Called ui_split_next() without ui_split_*()");
		ui_push();
	)
);

function ui_push_height(height) (
	height == -1 ? height = ui_text() + uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]*2;
	ui_push();
	height = min(height, uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] += (uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] - height)*uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = height;
	height;
);
function ui_push_heightratio(ratio) (
	ui_push_height(ratio*uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
);
function ui_push_heighttext(text) local(text_height) (
	text_height = max(ui_texth(), ui_text_height(text));
	ui_push_height(text_height + 2*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]);
);
function ui_push_width(width) (
	width == -1 ? width = ui_text()*4 + uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]*2;
	ui_push();
	width = min(width, uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] += (uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] - width)*uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = width;
);
function ui_push_widthratio(ratio) (
	ui_push_width(ratio*uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
);
function ui_push_widthtext(text) local(text_width) (
	text_width = ui_text_width(text);
	ui_push_width(text_width + 2*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]);
);

function ui_push_above(height) local(newheight) (
	height == -1 ? height = ui_text() + uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]*2;
	ui_push();
	newheight = (uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] - height)*uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/];
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = newheight;
	newheight;
);
function ui_push_aboveratio(ratio) (
	ui_push_above(ratio*uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
);
function ui_push_abovetext(text) local(text_height)  (
	text_height = max(ui_texth(), ui_text_height(text));
	ui_push_above(text_height + 2*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]);
);
function ui_push_below(height) local(newheight) (
	height == -1 ? height = ui_text() + uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]*2;
	ui_push();
	newheight = (uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] - height)*(1 - uix_drawconfig_current[14/*uix_DRAWCONFIG:VALIGN*/]);
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] = uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] + uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] - newheight;
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] = newheight;
	newheight;
);
function ui_push_belowratio(ratio) (
	ui_push_below(ratio*uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/]);
);
function ui_push_belowtext(text) local(text_height)  (
	text_height = max(ui_texth(), ui_text_height(text));
	ui_push_below(text_height + 2*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/]);
);
function ui_push_leftof(width) local(newwidth) (
	width == -1 ? width = ui_text()*4 + uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]*2;
	ui_push();
	newwidth = (uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] - width)*uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/];
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = newwidth;
	newwidth;
);
function ui_push_leftofratio(ratio) (
	ui_push_leftof(ratio*uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
);
function ui_push_leftoftext(text) local(text_width)  (
	text_width = ui_text_width(text);
	ui_push_leftof(text_width + 2*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]);
);
function ui_push_rightof(width) local(newwidth) (
	width == -1 ? width = ui_text()*4 + uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]*2;
	ui_push();
	newwidth = (uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] - width)*(1 - uix_drawconfig_current[13/*uix_DRAWCONFIG:HALIGN*/]);
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] = uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] - newwidth;
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] = newwidth;
	newwidth;
);
function ui_push_rightofratio(ratio) (
	ui_push_rightof(ratio*uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/]);
);
function ui_push_rightoftext(text) local(text_width)  (
	text_width = ui_text_width(text);
	ui_push_rightof(text_width + 2*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/]);
);

function ui_pad4(l, t, r, b) (
	l < 0 ? l = -l*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/];
	r < 0 ? r = -r*uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/];
	t < 0 ? t = -t*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/];
	b < 0 ? b = -b*uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/];
	uix_drawconfig_current[9/*uix_DRAWCONFIG:LEFT*/] += l;
	uix_drawconfig_current[10/*uix_DRAWCONFIG:TOP*/] += t;
	uix_drawconfig_current[11/*uix_DRAWCONFIG:WIDTH*/] -= l + r;
	uix_drawconfig_current[12/*uix_DRAWCONFIG:HEIGHT*/] -= t + b;
);

function ui_pad2(w, h) (
	ui_pad4(w, h, w, h);
);

function ui_pad1(n) (
	ui_pad4(n, n, n, n);
);

function ui_padtop() (
	ui_pad4(0, -1, 0, 0);
);

function ui_padbottom() (
	ui_pad4(0, 0, 0, -1);
);

function ui_padleft() (
	ui_pad4(-1, 0, 0, 0);
);

function ui_padright() (
	ui_pad4(0, 0, -1, 0);
);

function ui_pad() (
	ui_pad4(-1, -1, -1, -1);
);

function ui_padding(hpadding, vpadding) (
	hpadding >= 0 ? uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/] = hpadding;
	vpadding >= 0 ? uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/] = vpadding;
);

function ui_hpadding() (
	uix_drawconfig_current[15/*uix_DRAWCONFIG:HPADDING*/];
);

function ui_vpadding() (
	uix_drawconfig_current[16/*uix_DRAWCONFIG:VPADDING*/];
);

//////// Mouse functions ////////

function uix_click1wasinside() (
	!uix_mouseblocked && uix_click1_x >= ui_left() && uix_click1_x < ui_right() && uix_click1_y >= ui_top() && uix_click1_y < ui_bottom();
);

function ui_mouse_x() (
	mouse_x - ui_left();
);

function ui_mouse_xratio() (
	ui_mouse_x()/ui_width();
);

function ui_mouse_y() (
	mouse_y - ui_top();
);

function ui_mouse_yratio() (
	ui_mouse_y()/ui_height();
);

function ui_hover() (
	mouse_x >= ui_left() && mouse_x < ui_right() && mouse_y >= ui_top() && mouse_y < ui_bottom();
);

function ui_mouse_down() (
	uix_click1 && ui_hover();
);

function ui_mouse_down_outside() (
	uix_click1 && !ui_hover();
);

function ui_mouse_up() (
	uix_unclick1 && ui_hover();
);

function ui_click_clear() (
	uix_click_block();
);

function ui_drag() local(dragtime) (
	mouse_cap&1 && uix_click1wasinside() ? (
		dragtime = time_precise() - uix_click1_t;
		dragtime >= uix_doubleclicktime || abs(uix_click1_x - mouse_x) >= uix_draglimit || abs(uix_click1_y - mouse_y) >= uix_draglimit ? (
			dragtime;
		) : 0;
	) : 0;
);

function ui_drag_x() (
	mouse_cap&1 && uix_click1wasinside() ? (
		mouse_x - uix_click1_x;
	) : 0;
);

function ui_drag_y() (
	mouse_cap&1 && uix_click1wasinside() ? (
		mouse_y - uix_click1_y;
	) : 0;
);

function ui_mouse_dx() (
	mouse_x - uix_prev_mouse_x;
);


function ui_mouse_dy() (
	mouse_y - uix_prev_mouse_y;
);

function ui_press() (
	ui_hover() && mouse_cap&1 && uix_click1wasinside() ? (
		time_precise() - uix_click1_t;
	) : 0;
);

function ui_clickcount() (
	uix_click1count;
);

function ui_click() (
	ui_mouse_up() && uix_click1wasinside() && ui_hover() ? (
		time_precise() - uix_click1_t;
	) : 0;
);

function ui_key() (
	uix_latestchar;
);

function ui_key_printable() (
	uix_latestchar >= 32 && uix_latestchar < 127 ? (
		uix_latestchar;
	) : 0;
);

function ui_key_next() (
	uix_char_consumed = 1;
	uix_latestchar = gfx_getchar();
	uix_latestchar == -1 ? uix_latestchar = 0;
	uix_latestchar;
);

/////////////// LEVEL 2 ///////////////

uix_control_labelratio = 0.2;

function control_finish_enabled(strength) local(height, topalpha, bottomalpha) (
	// White fade
	height = ui_height()*0.5;
	topalpha = strength*0.3;
	bottomalpha = 0;
	gfx_gradrect(
		ui_left(), ui_top(), ui_width(), height,
		1, 1, 1, topalpha,
		0, 0, 0, 0,
		0, 0, 0, (bottomalpha - topalpha)/height
	);
	// Black fade
	height = ui_height()*0.5;
	topalpha = 0;
	bottomalpha = strength*0.1;
	gfx_gradrect(
		ui_left(), ui_bottom() - height, ui_width(), height,
		0, 0, 0, topalpha,
		0, 0, 0, 0,
		0, 0, 0, (bottomalpha - topalpha)/height
	);
	// Black fade 2
	height = ui_height()*0.2;
	topalpha = 0;
	bottomalpha = strength*0.1;
	gfx_gradrect(
		ui_left(), ui_bottom() - height, ui_width(), height,
		0, 0, 0, topalpha,
		0, 0, 0, 0,
		0, 0, 0, (bottomalpha - topalpha)/height
	);

	ui_colora(0, 0, 0, 0.1);
	ui_border();
);

function control_finish_disabled(strength) (
	ui_colora(0, 0, 0, 0.1);
	ui_border();
);

function control_finish_inset(strength) local(height, topalpha, bottomalpha) (
	// Black fade
	height = min(5, ui_height()/2);
	topalpha = strength*0.1;
	bottomalpha = 0;
	gfx_gradrect(
		ui_left(), ui_top(), ui_width(), height,
		0, 0, 0, topalpha,
		0, 0, 0, 0,
		0, 0, 0, (bottomalpha - topalpha)/height
	);
	// White fade
	height = min(5, ui_height()/2);
	topalpha = 0;
	bottomalpha = strength*0.3;
	gfx_gradrect(
		ui_left(), ui_bottom() - height, ui_width(), height,
		1, 1, 1, topalpha,
		0, 0, 0, 0,
		0, 0, 0, (bottomalpha - topalpha)/height
	);
	ui_colora(0, 0, 0, 0.1);
	ui_border();
);

function uix_circle_shade(x, y, r, angle, shadowstrength) (
	ui_push();
		ui_colora(255, 255, 255, 0.3);
		gfx_arc(x, y + 1, r, -1.5 + angle, 1.5 + angle, 1);
		gfx_arc(x, y + 1, r - 0.3, -1 + angle, 1 + angle, 1);
		gfx_a = 0.15;
		//gfx_arc(x, y + 2, r, -0.5 + angle, 0.5 + angle, 1);

		angle += $pi;
		ui_colora(0, 0, 0, 0.2*shadowstrength);
		gfx_arc(x, y - 1, r, -1.2 + angle, 1.2 + angle, 1);
		gfx_arc(x, y - 1, r, -0.8 + angle, 0.8 + angle, 1);
		gfx_a = 0.15;
		//gfx_arc(x, y - 2, r, -0.5 + angle, 0.5 + angle, 1);
		
		ui_colora(0, 0, 0, 0.1);
		gfx_circle(x, y, r, 0, 1);
	ui_pop();
);

function control_finish_enabled_circle(x, y, r) (
	uix_circle_shade(x, y, r, 0, 1);
);

function control_finish_inset_circle(x, y, r) (
	uix_circle_shade(x, y, r, $pi, 0.6);
);

function control_finish_disabled_circle(x, y, r) (
	ui_push();
		ui_colora(0, 0, 0, 0.1);
		gfx_circle(x, y, r, 0, 1);
	ui_pop();
);

function control_color_fill_disabled() (
	ui_color(180, 180, 180);
);

function control_color_fill_enabled() local(shade, red, green, blue) (
	shade = ui_press() ? 0.5 : ui_hover() ? 0.75 : ui_drag() ? 0.75 : 1;
	ui_color(64*shade, 102*shade, 140*shade);
);

function control_color_fill_inset() (
	ui_color(255, 255, 255);
);

function control_color_text_enabled() (
	ui_color(255, 255, 255);
);

function control_color_text_disabled() (
	ui_color(255, 255, 255);
);

function control_color_text_inset() (
	ui_color(0, 0, 0);
);

function control_color_border() (
	ui_colora(102, 102, 102, 0.5);
);

function control_border() (
	ui_push();
		control_color_border();
		ui_border();
	ui_pop();
);

function control_group(text) local(texth, textw, textl, textr, textv) (
	texth = textw = 0;
	uix_setgfxfont();
	gfx_measurestr(text, textw, texth);

	ui_pad2(ui_hpadding()/2, 0);
	
	ui_split_top(texth);
		ui_padleft();
		ui_split_left(textw + ui_hpadding());
			textl = ui_left();
			textr = ui_right();
			textv = ui_top() + ui_height()/2;
			ui_align(0.5, 0.5);
			control_color_border();
			ui_text(text);
		ui_pop();
	ui_pop();
	ui_padbottom();
	ui_push();
		control_color_border();
		gfx_x = textr;
		gfx_y = textv;
		gfx_lineto(ui_right(), gfx_y);
		gfx_lineto(gfx_x, ui_bottom());
		gfx_lineto(ui_left(), gfx_y);
		gfx_lineto(gfx_x, textv);
		gfx_lineto(textl, gfx_y);
	ui_pop();
	ui_pad4(-1, 0, -1, -1);
);

function control_readout(text) local() (
	ui_push();
		// Background color
		control_color_fill_inset();
		ui_fill();

		// Text
		control_color_text_inset();
		ui_text(text);
		// Highlight/shadows for depth effect
		control_finish_inset(1);
	ui_pop();
);

function control_indicator_button(text, status) local(click) (
	ui_push();
		status ? control_color_fill_enabled() : control_color_fill_disabled();
		ui_fill();

		status ? control_color_text_enabled() : control_color_text_disabled();
		ui_text(text);
		status ? control_finish_enabled(1) : control_finish_disabled(1);
		
		click = ui_click();
	ui_pop();
	click;
);

function control_button(text) (
	control_indicator_button(text, 1);
);

function control_arrow(direction) local(size, tmp, centerx, centery, point1x, point1y, point2x, point2y, point3x, point3y) ( // left, up, right, down
	size = min(ui_height(), ui_width());
	direction == 0 ? (
		centerx = size/2;
		centery = ui_height()/2;
	) : direction == 1 ? (
		centerx = ui_width()/2;
		centery = size/2;
	) : direction == 2 ? (
		centerx = ui_width() - size/2;
		centery = ui_height()/2;
	) : (
		centerx = ui_width()/2;
		centery = ui_height() - size/2;
	);
	centerx += ui_left();
	centery += ui_top();

	size = min(size, ui_texth());
	size *= 0.5;
	angle = direction*$pi/2;
	
	// Draw facing upwards to start
	point1x = 0;
	point1y = 0.5*size*(direction >= 2 ? 1 : -1);
	point2x = -0.5*size;
	point2y = -point1y;
	point3x = 0.5*size;
	point3y = -point1y;
	direction == 0 || direction == 2 ? (
		// Swap X and Y
		tmp = point1x;
		point1x = point1y;
		point1y = tmp;
		tmp = point2x;
		point2x = point2y;
		point2y = tmp;
		tmp = point3x;
		point3x = point3y;
		point3y = tmp;
	);

	ui_color_refresh();
	gfx_triangle(centerx + point1x, centery + point1y, centerx + point2x, centery + point2y, centerx + point3x, centery + point3y);
);

function control_selector(value, text, next_value, prev_value) local(buttonwidth) (
	ui_push();
		ui_color(255, 255, 255);
		ui_fill();
		buttonwidth = min(ui_height()/2, ui_width()/2);
		
		ui_halign() <= 0.5 ? ui_split_left(buttonwidth) : ui_split_right(buttonwidth);
			ui_colora(255, 255, 255, 0.8);
			ui_split_topratio(0.5);
				control_color_fill_enabled();
				ui_fill();
				control_color_text_enabled();
				control_arrow(1);
				control_finish_enabled(1);
				ui_click() ? value = next_value;
			ui_split_next();
				control_color_fill_enabled();
				ui_fill();
				control_color_text_enabled();
				control_arrow(3);
				control_finish_enabled(1);
				ui_click() ? value = prev_value;
			ui_pop();
		ui_pop();
		
		control_color_text_inset();
		ui_push();
			ui_padleft();
			ui_padright();
			ui_text(text);
		ui_pop();

		control_finish_inset(1);
	ui_pop();
	control_border();
	value;
);

function control_navbar(title, next_title, next_screen) local(click, direction) (
	direction = 0;
	ui_split_toptext(title);
		ui_color(255, 255, 255);
		ui_fill();

		ui_color(0, 0, 0);
		title ? (
			ui_text(title);
		);
		control_finish_enabled(0.5);
		control_border();

		ui_screen_level() > 0 ? (
			ui_split_lefttext("< back");
				control_button("< back") ? (
					direction = -1;
					ui_screen_close();
				);
			ui_pop();
		);
		(next_screen >= 0 || next_title >= 0) ? (
			next_title = next_title ? next_title : "next >";
			ui_split_righttext(next_title);
				control_button(next_title) ? (
					direction = 1;
					next_screen > 0 ? ui_screen_open(next_screen);
				);
			ui_pop();
		);
	ui_pop();
	direction
);

function uix_slider_value_to_ratio(value, range_low, range_high, curve_bias) local(valueratio) (
	valueratio = max(0, min(1, (value - range_low)/(range_high - range_low)));
	curve_bias ? (
		log(valueratio*(exp(curve_bias) - 1) + 1)/curve_bias
	) : valueratio;
);

function uix_slider_ratio_to_value(ratio, range_low, range_high, curve_bias) local(valueratio) (
	ratio = max(0, min(1, ratio));
	valueratio = curve_bias ? (
		(exp(ratio*curve_bias) - 1)/(exp(curve_bias) - 1)
	) : ratio;
	value = range_low + (range_high - range_low)*valueratio;
);

function control_hslider(value, range_low, range_high, curve_bias) local(valueratio, screenratio) (
	// We calculate screenratio first (before updating value) because then any rounding/limiting applied to the value is reflected in the UI
	screenratio = uix_slider_value_to_ratio(value, range_low, range_high, curve_bias);

	ui_push();
		control_color_fill_inset();
		ui_fill();
		control_finish_inset(1);

		ui_push();
			control_color_fill_enabled();
			barwidth = floor((ui_width() - 1)*screenratio + 1.5);
			ui_split_left(barwidth);
				ui_fill();
				control_finish_enabled(1);
			ui_pop();
		ui_pop();

		control_border();
	ui_pop();

	ui_mouse_down() || ui_drag() ? (
		value = uix_slider_ratio_to_value(ui_mouse_xratio(), range_low, range_high, curve_bias);
	);
	value;
);

function control_vslider(value, range_low, range_high, curve_bias) local(valueratio, screenratio) (
	// We calculate screenratio first (before updating value) because then any rounding/limiting applied to the value is reflected in the UI
	screenratio = uix_slider_value_to_ratio(value, range_low, range_high, curve_bias);

	ui_push();
		control_color_fill_inset();
		ui_fill();
		control_finish_inset(1);

		ui_push();
			control_color_fill_enabled();
			barheight = floor((ui_height() - 1)*screenratio + 1.5);
			ui_split_bottom(barheight);
				ui_fill();
				control_finish_enabled(1);
			ui_pop();
		ui_pop();

		control_border();
	ui_pop();

	ui_mouse_down() || ui_drag() ? (
		value = uix_slider_ratio_to_value(1 - ui_mouse_yratio(), range_low, range_high, curve_bias);
	);
	value;
);

function control_dragvalue(value, range_low, range_high, curve_bias) local(valueratio, deltay) (
	ui_drag() ? (
		valueratio = uix_slider_value_to_ratio(value, range_low, range_high, curve_bias);
		deltay = ui_mouse_dy()/200*exp(-abs(ui_drag_x())/200);
		value = uix_slider_ratio_to_value(valueratio - deltay, range_low, range_high, curve_bias);
	);
	value;
);

function control_dial(value, range_low, range_high, curve_bias)  local(valueratio, screenratio, centrex, centrey, radius, inner_radius, highangle, lowangle, angle) (
	screenratio = uix_slider_value_to_ratio(value, range_low, range_high, curve_bias);
	ui_push();
		radius = min(ui_width(), ui_height())/2 - 1;
		centrex = (ui_left() + radius) + (ui_width() - radius*2 - 1)*ui_halign();
		centrey = (ui_top() + radius) + (ui_height() - radius*2 - 1)*ui_valign();
		inner_radius = radius - 3;
		lowangle = -2.3;
		highangle = 2.3;

		angle = lowangle + (highangle - lowangle)*(screenratio*0.98 + 0.01);
		
		control_color_fill_enabled();
		gfx_circle(centrex, centrey, radius, 1, 1);
		
		control_color_text_enabled();
		gfx_arc(centrex, centrey, inner_radius, lowangle, angle, 1);
		gfx_arc(centrex + 1, centrey, inner_radius, lowangle, angle, 1);
		gfx_arc(centrex, centrey + 1, inner_radius, lowangle, angle, 1);
		gfx_arc(centrex - 1, centrey, inner_radius, lowangle, angle, 1);
		gfx_arc(centrex, centrey - 1, inner_radius, lowangle, angle, 1);
		
		control_finish_enabled_circle(centrex, centrey, radius);
	ui_pop();
	ui_pad1(5);
	control_color_text_enabled();

	control_dragvalue(value, range_low, range_high, curve_bias);
);

function control_dial_angle(angle) local(radius, centrex, centrey, inner_radius) (
	ui_push();
		radius = min(ui_width(), ui_height())/2 - 1;
		centrex = (ui_left() + radius) + (ui_width() - radius*2 - 1)*ui_halign();
		centrey = (ui_top() + radius) + (ui_height() - radius*2 - 1)*ui_valign();
		inner_radius = radius - 3;

		control_color_fill_enabled();
		gfx_circle(centrex, centrey, radius, 1, 1);
		
		control_color_text_enabled();
		
		gfx_circle(centrex + sin(angle)*inner_radius, centrey - cos(angle)*inner_radius, 1.5, 1, 1);

		control_finish_enabled_circle(centrex, centrey, radius);
	ui_pop();
	ui_pad1(5);
	control_color_text_enabled();
	
	angle = control_dragvalue(angle, -0.1, 2*$pi + 0.1, 0);
	angle < 0 ? angle += 2*$pi;
	angle >= 2*$pi ? angle -= 2*$pi;
	angle;
);

function control_dialog(title, width, height, doneText, cancelText) local(accepted) (
	accepted = 0;
	ui_push();
		ui_colora(0, 0, 0, 0.2);
		ui_fill();
	ui_pop();

	!width ? width = ui_width()*0.8;
	!height ? height = ui_height()*0.8;
	
	title >= 0 ? height += ui_text_height(title) + ui_vpadding()*2;
	
	width = min(width + ui_hpadding()*2, ui_width());
	height = min(height + ui_vpadding()*2, ui_height());
	ui_pad2((ui_width() - width)/2, (ui_height() - height)/2);
	ui_push();
		ui_color(242, 242, 242);
		ui_fill();
	ui_pop();

	doneText >= 0 || cancelText >= 0 ? (
		ui_split_bottomtext(-1);
			cancelText >= 0 && doneText < 0 ? (
				control_color_fill_disabled();
				ui_fill();
				control_color_text_disabled();
				ui_text(cancelText);
				control_finish_enabled(1);
				
				ui_click() ? accepted = -1;
			) : cancelText >= 0 ? (
				ui_push();
					ui_split_leftratio(0.5);
						control_button(doneText) ? accepted = 1;
					ui_split_next();
						control_color_fill_disabled();
						ui_fill();
						control_color_text_disabled();
						ui_text(cancelText);
						control_finish_enabled(1);
						
						ui_click() ? accepted = -1;
					ui_pop();
				ui_pop();
			) : (
				control_button(doneText) ? accepted = 1;
			);
			control_color_border();
			ui_border_left();
			ui_border_right();
			ui_border_bottom();
		ui_pop();
	);
	
	title >= 0 ? (
		ui_split_toptext(title);
			ui_color(255, 255, 255);
			ui_fill();
			ui_color(0, 0, 0);
			ui_text(title);
			control_finish_enabled(0.5);
			control_color_border();
			ui_border_left();
			ui_border_right();
			ui_border_top();
		ui_pop();
	);
	
	control_color_border();
	ui_border();
	
	ui_pad();
	accepted ? ui_screen_close();
	accepted;
);

function uix_control_closest_substring_width(text, targetwidth) local(text_length, best_index, best_width, i, candidatewidth) (
	best_index = 0;
	best_width = 0;
	
	i = 0;
	text_length = strlen(text);
	while (i <= text_length) (
		strcpy_substr(#uix_control_closest_substring_width, text, 0, i);
		candidatewidth = ui_text_width(#uix_control_closest_substring_width);
		abs(candidatewidth - targetwidth) <= abs(best_width - targetwidth) ? (
			best_width = candidatewidth;
			best_index = i;
		);
		i += 1;
	);
	best_index;
);

function uix_autoselect_char(char) (
	// Alphanumeric (plus "." and "_") in the latin1 charset
	char == '.' || char == '_' || (char >= '0' && char <= '9') || (char >= 'A' && char <= 'Z') || (char >= 'a' && char <= 'z')
		|| (char >= 192 && char <= 255);
);

function control_textinput_hasfocus(prompt_selectpacked) (
	prompt_selectcursor = floor(prompt_selectpacked) - 1;
	prompt_selecttrail = floor((prompt_selectpacked - 1 - prompt_selectcursor)*(text_length + 2) + 0.5) - 1;
	prompt_selecttrail >= 0;
);

function control_textinput_focus(prompt_selectpacked) (
	prompt_selectcursor = floor(prompt_selectpacked) - 1;
	prompt_selecttrail = floor((prompt_selectpacked - 1 - prompt_selectcursor)*(text_length + 2) + 0.5) - 1;
	
	prompt_selecttrail = (prompt_selectcursor < 0) ? 0 : prompt_selectcursor;
	
	prompt_selectpacked = prompt_selectcursor + (prompt_selecttrail + 1)/(text_length + 2) + 1;
	prompt_selectpacked;
);

function control_textinput_unfocus(prompt_selectpacked) (
	prompt_selectcursor = floor(prompt_selectpacked) - 1;
	prompt_selecttrail = floor((prompt_selectpacked - 1 - prompt_selectcursor)*(text_length + 2) + 0.5) - 1;
	
	prompt_selecttrail = -1;
	
	prompt_selectpacked = prompt_selectcursor + (prompt_selecttrail + 1)/(text_length + 2) + 1;
	prompt_selectpacked;
);

function control_textinput(text, prompt_selectpacked) local(text_length, prompt_selectlow, prompt_selecthigh, textx, texty, selectw, t, char, keyinterested, char_index) (
	text_length = strlen(text);
	prompt_selectcursor = floor(prompt_selectpacked) - 1;
	prompt_selecttrail = floor((prompt_selectpacked - 1 - prompt_selectcursor)*(text_length + 2) + 0.5) - 1;
	prompt_selectcursor < 0 ? (
		prompt_selectcursor = text_length;
		prompt_selecttrail >= 0 ? prompt_selecttrail = prompt_selectcursor;
	);
	prompt_selectcursor = min(text_length, prompt_selectcursor);
	prompt_selecttrail = max(-1, min(text_length, prompt_selecttrail));
	
	prompt_selecthigh = max(prompt_selectcursor, prompt_selecttrail);
	prompt_selectlow = prompt_selecttrail >= 0 ? min(prompt_selectcursor, prompt_selecttrail) : prompt_selecthigh;
	prompt_selectcursor < 0 ? (
		prompt_selectcursor = text_length;
		prompt_selecttrail = -1;
	);

	ui_push();
		ui_align(0, 0.5);
		control_color_fill_inset();
		ui_fill();

		ui_mouse_down() ? (
			char_index = uix_control_closest_substring_width(text, ui_mouse_x() - ui_hpadding());
			(mouse_cap&8) ? ( // Shift
				prompt_selecttrail = char_index;
			) : (
				prompt_selectcursor = char_index;
				prompt_selecttrail = prompt_selectcursor;
			);
			uix_control_cursortimeoffset = time_precise();
		);
		ui_drag() ? (
			prompt_selecttrail = uix_control_closest_substring_width(text, ui_mouse_x() - ui_hpadding());
			uix_control_cursortimeoffset = time_precise();
		);
		(ui_drag() || ui_click()) && ui_clickcount() == 2 ? (
			while (prompt_selectlow > 0 && uix_autoselect_char(str_getchar(text, prompt_selectlow - 1))) (
				prompt_selectlow -= 1;
			);
			while (prompt_selecthigh < text_length && uix_autoselect_char(str_getchar(text, prompt_selecthigh))) (
				prompt_selecthigh += 1;
			);
			
			ui_click() ? (
				ui_click_clear();
				prompt_selectcursor = prompt_selectlow;
				prompt_selecttrail = prompt_selecthigh;
			);
		);
		ui_mouse_down_outside() ? (
			prompt_selectcursor = text_length;
			prompt_selecttrail = -1; // No cursor
		);
		
		control_color_text_inset();
		ui_push();
			ui_pad();
			ui_font_refresh();
			textx = ui_left();
			texty = ui_top() + (ui_height() - gfx_texth)/2;
			gfx_x = textx;
			gfx_y = texty;

			strcpy_substr(#control_tmp1, text, 0, prompt_selectlow);
			gfx_drawstr(#control_tmp1);
			
			prompt_selecthigh > prompt_selectlow ? (
				strcpy_substr(#control_tmp1, text, prompt_selectlow, prompt_selecthigh - prompt_selectlow);
				selectw = ui_text_width(#control_tmp1);
				ui_push();
					control_color_fill_enabled();
					gfx_rect(gfx_x, gfx_y, selectw, gfx_texth);
					control_color_text_enabled();
					gfx_drawstr(#control_tmp1);
				ui_pop();
				ui_font_refresh();
			) : (
				t = time_precise() - uix_control_cursortimeoffset;
				t = t - floor(t);
				prompt_selecttrail == prompt_selectcursor ? t < 0.5 ? (
					gfx_line(gfx_x, gfx_y, gfx_x, gfx_y + gfx_texth);
				);
			);

			strcpy_from(#control_tmp1, text, max(prompt_selectlow, prompt_selecthigh));
			gfx_drawstr(#control_tmp1);
		ui_pop();
		
		control_finish_inset(1);
		
		prompt_selecttrail >= 0 ? (
			keyinterested = prompt_selecttrail >= 0;
			while (keyinterested && ui_key()) (
				char = ui_key();
				ui_key_printable() ? (
					strcpy_substr(#control_tmp1, text, 0, prompt_selectlow);
					str_setchar(#control_tmp1, prompt_selectlow, char);
					strcpy_from(#control_tmp2, text, prompt_selecthigh);
					strcat(#control_tmp1, #control_tmp2);
					strcpy(text, #control_tmp1);
					
					prompt_selectcursor += 1;
					prompt_selecttrail = prompt_selectcursor;
					text_length = strlen(text);
				) : char == 8 || char == 6579564 ? ( // Backspace and Delete
					prompt_selectlow == prompt_selecthigh ? (
						char == 8 ? (
							prompt_selectlow = max(prompt_selectlow - 1, 0);
						) : (
							prompt_selecthigh = min(text_length, prompt_selecthigh + 1);
						);								
					);
					strcpy_substr(#control_tmp1, text, 0, prompt_selectlow);
					strcpy_from(#control_tmp2, text, prompt_selecthigh);
					strcat(#control_tmp1, #control_tmp2);
					strcpy(text, #control_tmp1);

					prompt_selectcursor = prompt_selecttrail = prompt_selectlow;
					text_length = strlen(text);
				) : char == 1919379572 ? ( // Right
					(mouse_cap&8) ? ( // Shift
						prompt_selectcursor = min(prompt_selectcursor + 1, text_length);
					) : (
						prompt_selectlow == prompt_selecthigh ? (
							// Move cursor right
							prompt_selectcursor = min(prompt_selectcursor + 1, text_length);
						) : (
							// Move to right of selection
							prompt_selectcursor = prompt_selecthigh;
						);
						prompt_selecttrail = prompt_selectcursor;
					);
				) : char == 1818584692 ? ( // Left
					(mouse_cap&8) ? ( // Shift
						prompt_selectcursor = max(prompt_selectcursor - 1, 0);
					) : (
						prompt_selectlow == prompt_selecthigh ? (
							// Move cursor left
							prompt_selectcursor = max(prompt_selectcursor - 1, 0);
						) : (
							// Move to left of selection
							prompt_selectcursor = prompt_selectlow;
						);
						prompt_selecttrail = prompt_selectcursor;
					);
				) : char == 1752132965 ? ( // Home
					prompt_selectcursor = 0;
					(!(mouse_cap&8)) ? (
						prompt_selecttrail = prompt_selectcursor;
					);
				) : char == 6647396 ? ( // End
					prompt_selectcursor = text_length;
					(!(mouse_cap&8)) ? (
						prompt_selecttrail = prompt_selectcursor;
					);
				) : char == 1 ? ( // Ctrl+A
					prompt_selectcursor = 0;
					prompt_selecttrail = text_length;
				) : (
					keyinterested = 0;
				);
				keyinterested ? (
					ui_key_next();
					uix_control_cursortimeoffset = time_precise();
				);
			);
		);
	ui_pop();

	prompt_selectpacked = prompt_selectcursor + (prompt_selecttrail + 1)/(text_length + 2) + 1;
	prompt_selectpacked;
);

function control_system() local(prompt_title, prompt_text, prompt_init, prompt_selectlow, prompt_selecthigh, textx, texty, selectw, t, char, OK) (
	ui_screen() == "control.prompt" ? (
		prompt_text = ui_screen_get(0);
		prompt_title = ui_screen_get(1);
		prompt_init = ui_screen_get(2);
		prompt_selectpacked = ui_screen_get(3);

		!prompt_init ? (
			while (gfx_getchar()) (
				0; // Drop keys from queue
			);
			prompt_init = 1;
			strcpy(#control_prompt_text, prompt_text);
			prompt_selectpacked = control_textinput_focus(prompt_selectpacked);
		);

		OK = control_dialog(prompt_title, 0, (ui_texth() + ui_vpadding()*2)*3, "done", "cancel");

		ui_push_heighttext(-1);
			prompt_selectpacked = control_textinput(#control_prompt_text, prompt_selectpacked);
		ui_pop();

		// Confirm/exit
		ui_key() == 13 ? (
			OK = 1;
		) : ui_key() == 27 ? (
			OK = -1;
		);

		ui_screen_set(2, prompt_init);
		ui_screen_set(3, prompt_selectpacked);

		OK ? (
			(OK == 1) ? strcpy(prompt_text, #control_prompt_text);
			ui_screen_close_id("control.prompt");
		);
	) : ui_system();
);
